=title DBI와 SQL을 이용한 간편한 Database접근
=timestamp 2013-05-21T08:32:47
=indexes SQL, DBI, DBD::SQLite, SELECT, fetchrow_array, fetchrow_hashref
=status show
=original simple-database-access-using-perl-dbi-and-sql
=author szabgab
=translator yongbin
=index 1
=feed 1
=comments_disqus_enable 1

=abstract start

Perl의 특징적인 문화중 하나로 "모든 일을 하는데는 한가지 이상의 방법이 있다" - <a href="http://en.wikipedia.org/wiki/There%27s_more_than_one_way_to_do_i">TMTOWTDI</a>로 대변되는 자유로움이 있습니다. 하지만 관계형 데이터베이스 접근방법에 있어서 만큼은 <b>사실상 산업계의 표준이 된 일관된 방식의 표준 라이브러리인 DBI(Database independent interface for Perl)를 제공</b>하고 있습니다. 오늘은 이 DBI에 대해서 알아보도록 하겠습니다.

=abstract end

<h2>구조</h2>

Perl 프로그램에서 DBI를 사용해서 DB에 접근하면 DBI는 적절한 <b>데이타베이스 드라이버</b>를 불러옵니다. (예를 들면 <a href="http://www.oracle.com/">Oracle</a>에 접속을 시도하면 <a href="https://metacpan.org/module/DBD::Oracle">DBD::Oracle</a>, <a href="http://www.postgresql.org/">PostgreSQL</a>에 접속하면 <a href="https://metacpan.org/module/DBD::Pg">DBD::Pg</a>, <a href="http://sqlite.org/">SQLite</a>에 접속하면 <a href="https://metacpan.org/module/DBD::SQLite">DBD::SQLite</a>)

이런 드라이버는 주로 해당 데이터베이스 엔진에 맞는 C 클라이언트 라이브러리로 컴파일되는 형태로 제공됩니다. SQLite의 경우 DB자체가 가벼운 특징을 가지고 있기 때문에 드라이버를 불러들이면 데이터베이스 엔진 전체가 Perl 응용프로그램으로 적제됩니다.

이런 DBI의 구조를 ASCII로 가장 잘 표현한 도식은 <a href="https://metacpan.org/module/DBI">DBI 공식문서</a>에서 표현한 아래 내용입니다.

<pre>
             |<- Scope of DBI ->|
                  .-.   .--------------.   .-------------.
  .-------.       | |---| XYZ Driver   |---| XYZ Engine  |
  | Perl  |       | |   `--------------'   `-------------'
  | script|  |A|  |D|   .--------------.   .-------------.
  | using |--|P|--|B|---|Oracle Driver |---|Oracle Engine|
  | DBI   |  |I|  |I|   `--------------'   `-------------'
  | API   |       | |...
  |methods|       | |... Other drivers
  `-------'       | |...
                  `-'
</pre>

<h2>Simple example</h2>

I'll use SQLite to demonstrate the examples. That will make it
very easy for you to try them on your computer.
(e.g All versions of <a href="http://dwimperl.com/">DWIM Perl</a> already come
with the necessary modules.)

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use DBI;

my $dbfile = "sample.db";

my $dsn      = "dbi:SQLite:dbname=$dbfile";
my $user     = "";
my $password = "";
my $dbh = DBI->connect($dsn, $user, $password, {
   PrintError       => 0,
   RaiseError       => 1,
   AutoCommit       => 1,
   FetchHashKeyName => 'NAME_lc',
});

# ...

$dbh->disconnect;
</code>

We load DBI but we do <b>not</b> explicitly load the database driver. That will be done by DBI.

The <b>DSN (Data Source Name)</b> (in the $dsn variable) is very straight forward.
It contains the type of the database. That will be the clue to DBI which DBD to load.
In case of SQLite, the only thing we really need is the path to the database file.

The username and password fields were left empty. I think they are not
relevant at all for SQLite.

The last parameter of the connect call is a reference to a hash containing
some attributes I like to set.

The DBI->connect call returns a <b>database handle object</b> that usually we store
in a variable called <hl>$dbh</hl>.

The call to <b>disconnect</b> from the database is optional as it
will automatically be called when the variable <hl>$dbh</hl> goes out
of scope, but it having it might be a clear indication for
the next programmer dealing with this code, that you are done with the database.

<h2>CREATE TABLE</h2>

Of course having a connection is not enough. We need to be able to fetch data from the
database, or insert data into the database, but for our example to work first we
actually need to create the tables of the database.

In this case we can do this with a single command:

<code lang="perl">
my $sql = <<'END_SQL';
CREATE TABLE people (
  id       INTEGER PRIMARY KEY,
  fname    VARCHAR(100),
  lname    VARCHAR(100),
  email    VARCHAR(100) UNIQUE NOT NULL,
  password VARCHAR(20)
)
END_SQL

$dbh->do($sql);
</code>

The first statement is just a <a href="/here-document">here document</a> of an
SQL statement to CREATE TABLE.
Then we call the <hl>do</hl> method of the database handle which will send the
SQL statement to the database.

<h2>INSERT</h2>

Now let's see the real thing, inserting data:

<code lang="perl">
my $fname = 'Foo';
my $lname = 'Bar',
my $email = 'foo@bar.com';
$dbh->do('INSERT INTO people (fname, lname, email) VALUES (?, ?, ?)',
  undef,
  $fname, $lname, $email);
</code>

To insert a row we call the <hl>$dbh->do</hl> method again, but instead of passing
the actual data, we put question-marks <hl>?</hl> as <b>place-holders</b>.

The SQL statement is followed by the word <hl>undef</hl>.
That's actually the place of a hash-reference providing
parameters to this specific call, similar to the attributes
passed to the <hl>connect</hl> method, but I think it is rarely used in these
statements.

The <hl>undef</hl> is followed by the actual values that go in place of the the place-holders.

As you can see we did not have to put the place-holders in any kind of quotes
nor did we have to somehow convert the values. DBI did it for us.

This helps us avoid
<a href="http://en.wikipedia.org/wiki/Sql_injection">SQL injection</a> attacks.
Even if you meet someone called <a href="http://bobby-tables.com/">Bobby Tables</a>.

<h2>UPDATE</h2>

For updating some data in the database we also use the <hl>do</hl> method.

<code lang="perl">
my $password = 'hush hush';
my $id = 1;

$dbh->do('UPDATE people SET password = ? WHERE id = ?',
  undef,
  $password,
  $id);
</code>

Nothing special here. An SQL statement with some place-holders. <hl>undef</hl>
instead of the extra attributes, and the parameters to be used in place of
the place-holders.

<h2>SELECT</h2>

This is by far the most interesting part of the database access. As the SELECT
statement can return a lot of rows and a lot of values in each row we cannot use
a simple call to the <hl>do</hl> method.

Instead, there are several ways to fetch the data. I'll show here two.
For both we have 3 steps: <hl>prepare</hl> the SQL statement,
<hl>execute</hl> the statement with specific data, and <hl>fetch</hl> the rows.

From these, the <hl>prepare</hl> statement can be shared by - assuming the queries
only differ in the data we pass to them. We create an SQL statement using
question marks (<hl>?</hl>) as place-holders instead of actual values.

This call returns a <b>statement handle object</b> that we usually save in a
variable called <hl>$sth</hl>.

Then we call the <hl>execute</hl> method of the <b>statement handle</b> passing to
it values that should be placed instead of the place-holders.

The third step is the really interesting one.
In a <b>while loop</b> we fetch the results, row-by row. For this we can use several methods:

The <hl>fetchrow_array</hl> method will return the values of the next row in the result set
as a list, that we can assign to an array. The order of the elements is as the order
of the fields in the query. (fname, lname in our case).

The <hl>fetchrow_hashref</hl> method will return a reference to a hash. In the hash
the keys will be the names of the fields in the database. As different databases might
return these names of the fields in different cases we configured our database handler
to make sure they - the names of the fields - will always converted to lower case.
(That's what the <hl>FetchHashKeyName</hl> parameter did, when connecting to the database.)

<code lang="perl">
my $sql = 'SELECT fname, lname FROM people WHERE id > ? AND id < ?';
my $sth = $dbh->prepare($sql);
$sth->execute(1, 10);
while (my @row = $sth->fetchrow_array) {
   print "fname: $row[0]  lname: $row[1]\n";
}

$sth->execute(12, 17);
while (my $row = $sth->fetchrow_hashref) {
   print "fname: $row->{fname}  lname: $row->{lname}\n";
}
</code>


<h2>Exercise</h2>

Take the above snippets of code. Use  the first one to set up the
database and create a table. Then use the second one to insert
a few people in the table.

Finally use the last example to extract some data from the database
and print it out.

If you have any question, feel free to ask below.

<h2>Thanks</h2>

to sigzero for correcting a bug in the examples!

