=title Can't call method ... on unblessed reference
=timestamp 2014-04-03T08:10:01
=indexes B::Deparse
=status show
=books beginner_book
=author szabgab
=archive 1
=comments 1
=social 1

=abstract start

A while ago I got an e-mail from one of the readers with the following question:

<blockquote>
What is the difference between <hl>subname $param</hl> and <hl>subname($param)</hl>?

One seems to work, the other throws an error <hl>Can't call method ... on unblessed reference</hl>.
</blockquote>

In other words:  <b>do you have to put parentheses around the parameters of function calls?</b>

=abstract end

Let's look at some things:

<h2>require</h2>

If we run the following script we will get an error:
<hl>Can't call method "Storable::freeze" on unblessed reference at ...</hl> pointing to the line
where we call <hl>Storable::freeze $data;</hl>.

<code lang="perl">
use strict;
use warnings;

require Storable;

my $data = { a => 42 };
my $frozen = Storable::freeze $data;
</code>


If we change the problematic line and put the parameters in parentheses:
<hl>my $frozen = Storable::freeze($data);</hl>, everything works fine.


<h2>B::Deparse</h2>

Let's try to use B::Deparse again. Once it already
<a href="/bug-in-the-for-loop-b-deparse-to-the-rescue">helped us with the for loop</a>.


Let's run the first version of the script using <hl>perl -MO=Deparse  store.pl</hl>:

<code lang="perl">
use warnings;
use strict;
require Storable;
my $data = {'a', 42};
my $frozen = $data->Storable::freeze;
</code>

and the the second version of the script:

<code lang="perl">
use warnings;
use strict;
require Storable;
my $data = {'a', 42};
my $frozen = Storable::freeze($data);
</code>

The second version returned exactly the same code as we had in our file.
(Well, except that it replaced the <a href="/perl-hashes">fat-arrow</a> by a comma, but we know they are the same.)

On the other hand, in the first script, the critical part of the code was changed to 
<hl>$data->Storable::freeze;</hl>.

Perl thought that we are calling the <hl>Storable::freeze</hl> method on the <hl>$data</hl> object.
This code would work if the <hl>$data</hl> was a <a href="/pro/getting-started-with-classic-perl-oop">blessed reference</a>,
though it would probably do something different than what we meant.

<h2>use</h2>

If we replace the <hl>require Storable;</hl> by <hl>use Storable;</hl> then both cases work correctly,
and in both cases B::Deparse will show the code as being <hl>Storable::freeze($data);</hl>

<h2>eval "use Storable";</h2>

If instead of calling <hl>use</hl> directly we wrap it in an <hl>eval</hl> statement with a string
after it, the problem appears again.

<h2>Explanation</h2>

<hl>use</hl> happens at compile time. By the time perl sees the call to <hl>Storable::freeze</hl> it has
already loaded the <hl>Storable</hl> module.

<hl>require</hl> and <hl>eval "use ...";</hl> happens at run time, so when perl compiles the
<hl>Storable::freeze $data</hl> call, it does not know that there is going to be a <hl>Storable::freeze</hl>
functions. At this point it needs to guess what this code is, and it guesses, incorrectly,
that we are using the so-called <b>indirect object notation</b>. It thinks <hl>$data</hl> is going to be
an object and <hl>Storable::freeze</hl> is a method of this object.

That's why B::Deparse shows this code as <hl>$data->Storable::freeze;</hl>.

<h2>Solution</h2>

In order to eliminate the error you need to make sure the functions you use are already defined or loaded
before you actually use them. Putting parentheses after the function call eliminates this restriction,
and allows you put the function call wherever you want.


