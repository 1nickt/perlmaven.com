=title Uploading files using Dancer
=timestamp 2016-04-11T20:30:01
=indexes Dancer2::Core::Request::Upload
=status show
=books dancer2
=author szabgab
=archive 1
=comments_disqus_enable 1

=abstract start

Uploading images, or files in general was always a hot issue with all the web frameworks.
<a href="/dancer">Dancer</a> makes it quite easy.

=abstract end

<h2>Create the HTML form for upload</h2>

Save this as <b>views/upload.tt</b>

<code lang="html">
<form action="/upload" method="POST" enctype="multipart/form-data">
<input type="file" name="file">
<input type="submit" name="submit" value="Upload">
</form>
</code>

In the module implementing the Dancer2 application add the following two routes:

<code lang="perl">
get '/upload' => sub {
    template 'upload';
};

post '/upload' => sub {
    my $data = request->upload('file');

    my $dir = path(config->{appdir}, 'uploads');
    mkdir $dir if not -e $dir;

    my $path = path($dir, $data->basename);
    if (-e $path) {
        return "'$path' already exists";
    }
    $data->link_to($path);
    return "Uploaded";
};
</code>

The first route will just display the upload page with the HTML form that allows
the user to select a filename then it will access the second route using <hl>POST</hl>
method. The <hl>request->upload('file');</hl> will fetch the name of the 'file'
field of the HTML form, upload the file to a temporary directory and return and
instance of
<a href="http://metacpan.org/pod/Dancer2::Core::Request::Upload">Dancer2::Core::Request::Upload</a>
representing the uploaded file. This object has several methods to interact with then
information. We used the <hl>basename</hl> method to fetch the original name of the
file uploaded.

We used <hl>config->{appdir}</hl> to get the path where the whole Dancer2 application
is installed. This is the root of our application. Inside the root we create a directory
called "uploads" if it does not exists.

Then we use the <hl>basename</hl> of the original file to decide what will be the name
of the file on the server. (Actually this is probably not a good idea because it gives too much
power to the users of the system. It would be probably better to create a random and unique name
for the file and use some kind of database to store the connection between the original name
and the name used on the server.)

Then we use the <hl>link_to</hl> method to actually move the file from its temporary location
to the the place and name we want it to be.


<h2>Full example with test</h2>

<a href="https://code-maven.com/slides/dancer/upload-a-file">Uploading a file using Dancer</a>

<a href="https://code-maven.com/slides/dancer/testing-upload-a-file">Testing uploadig a file using Dancer</a>

