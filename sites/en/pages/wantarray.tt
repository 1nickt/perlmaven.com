=title wantarray - returning list or scalar based on context
=timestamp 2014-04-27T20:30:01
=indexes wantarray
=status show
=books advanced_book
=author szabgab
=comments_disqus_enable 1

=abstract start

You are already familiar with the <a href="/scalar-and-list-context-in-perl">LIST and SCALAR context</a> of Perl and while you might still remember
how confusing it was when you first encountered it, but you feel that function you are currently writing would greatly
benefit from that flexibility.

In other words, you'd like to create a function that will return different values depending on the context it was used in.

=abstract end

For example you'd like to create a function that given N, a whole number will compute the first N values of the Fibonacci function.
In scalar context it will return the value of <hl>fibo(N)</hl>. In list context it will return the whole series of N numbers.

<h2>VOID context</h2>

Before implementing it, we have to mention one more thing, that has not been stressed earlier. Perl actually has 3 major contexts.

LIST, SCALAR and VOID context. This last one is created when the value of an expression is not used, when a function is called but no
one checks the return value. The most common statements we usually use in VOID context are <hl>print</hl>, <hl>say</hl>, and <hl>chomp</hl>.
It is quite rare in Perl code that someone checks if a call to <hl>print</hl> was successful, and checking the return value of
<hl>chomp</hl> usually happens to people who don't yet understand how <a href="/chomp">chomp works</a>.

So the three contexts are created like this:

<code lang="perl">
my @series = fibo($n);   # LIST
my $value  = fibo($n);   # SCALAR
fibo($n);                # VOID
</code>

<h2>wantarray</h2>

When the developer of the <hl>fibo</hl> function implements the function, she cannot know in which context it will be executed,
but she can use the <hl>wantarray</hl> built-in inside the definition of the <hl>fibo</hl> function to check the current context of each call.

<hl>wantarray</hl> itself has 3 different return values:
<ul>
  <li>it can return <a href="/undef-and-defined-in-perl">undef</a></li>
  <li>it can return some <a href="/boolean-values-in-perl">false</a> value, other than undef.</li>
  <li>it can return <a href="/boolean-values-in-perl">true</a> value</li>
</ul>

These return values map to the 3 cases of how the function in which we use <hl>wantarray</hl> was called:

wantarray will return
<ul>
  <li><a href="/undef-and-defined-in-perl">undef</a> if the function was called in VOID context</li>
  <li><a href="/boolean-values-in-perl">false</a>, other than undef if the function was called in SCALAR context</li>
  <li><a href="/boolean-values-in-perl">true</a> if the function was called in LIST context.</li>
</ul>

Here is the skeleton of the code with 3 different invocations:

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub fibo {
   if (wantarray) {
       say 'LIST';
   } elsif (defined wantarray) {
       say  'SCALAR';
   } else {
       say  'VOID';
   }
}

my @numbers = fibo();  # LIST
my $value   = fibo();  # SCALAR
fibo();                # VOID
</code>


And now let's implement the Fibonacci:

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub fibo {
   my ($n) = @_;

   die 'There is no point in calling fibo() in VOID context'
       if not defined wantarray;

   my @fibo = (1, 1);
   push @fibo, $fibo[-1] + $fibo[-2] for 3 .. $n;

   return wantarray ? @fibo : $fibo[-1];
}

my @numbers = fibo(4);
say "@numbers";
my $value   = fibo(5);
say $value;
fibo(100);
</code>

It is written slightly differently from the skeleton.
The first thing we check is if <hl>wantarray</hl> returns undef. If it does, meaning the user
was not interested in the return value, we can assume it was a mistake. At this point we can
just call <hl>return</hl> without making any noise. We could call <hl>warn</hl> and then return,
or, as in the above example, we can call <hl>die</hl> and throw an exception.

The next two lines are the lines where we actually calculate the first N numbers of the Fibonacci series.

Then comes the call to <hl>return</hl> either the whole array, or just the last element using
the <a href="/the-ternary-operator-in-perl">ternary operator</a>.

The result looks like this:

<code>
$ perl fibonacci.pl 
1 1 2 3
5
There is no point in calling fibo() in VOID context at fibonacci.pl line 9.
</code>

<h2>Conclusion and comments</h2>

While I think the context sensitivity in Perl is very interesting, it can take time till a new Perl programmer comprehends it.
Adding such behavior to user-defined function can make the API of a module better, but it can also make it harder to
understand. <b>Use it with caution!</b>

The name <hl>wantarray</hl> is unfortunately misleading.
For many new Perl programmers it takes some time to understand what is the difference between ARRAYS and LISTS,
the fact that the function called <hl>wantarray</hl> actually checks if the enclosing function was called in LIST context
just adds to this confusion. On the other hand, beginner Perl programmers probably should not use the <hl>wantarray</hl> function.

Finally, Perl actually has more fine-grained contexts, for example <a href="/what-are-string-and-numeric-contexts">string and numeric context</a>.
In another article we'll see how we can create a function that will aware of those contexts as well.




