=title Moose-like type checking system for Moo
=timestamp 2015-05-05T08:00:01
=indexes Moo, MooX::late
=books moo
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

Unlike its bigger brother, Moo does not come with a built-in type system.
Instead, it allows us to add <a href="/type-checking-with-moo">home-made subroutines</a>
to each attribute that will check the type when we call the constructor or a setter.

MooX::late is a an extension for Moo, that provides several features available in
<a href="/moose">Moose</a>, but not in <a href="/moo">Moo</a>.

For example, it allows us to set type-constraints in a declarative way
using <a href="http://metacpan.org/module/Types::Standard">Types::Standard</a>
as the back-end.

Let's see a bit more details how does that work:

=abstract end

<h2>No constraint</h2>

Let's start by an example without any constraint.

The <hl>Person.pm</hl> file contains the following code:

<code lang="perl">
package Person;
use 5.010;
use Moo;

has name => (is => 'rw');

1;
</code>

The <hl>person.pl</hl> file in the same directory contains the following:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;
my $student = Person->new(name => 'Foo');
say $student->name;
</code>

If we run <hl>perl person.pl</hl> it will print <hl>Foo</hl>.
There is nothing special about this.

<h2>Introducing type constraints</h2>

The person will have a height as well, and we would like to make sure
the hight is always set as an integer number. (We just need to
decide if we measure in cm or inches.)

Add this to <hl>Person.pm</hl>

<code lang="perl">
use MooX::late;

has height => (is => 'rw', isa => 'Int');
</code>

And change the script <hl>person.pl</hl> to this:

<code lang="perl">
use Person;
my $student = Person->new(name => 'Foo', height => 180);
say $student->name;
say $student->height;
</code>

Running the script will print out the name and height as expected.
Still nothing interesting. So let's change the script now and try
this code:

<code lang="perl">
my $student = Person->new(name => 'Foo', height => 'tall');
</code>

Running <hl>perl person.pl</hl> will generate an exception:

<hl>Value "tall" did not pass type constraint "Int" (in $self->{"height"}) at (eval 230) line 39.</hl>

It is clear what is the problem, but unfortunately it does not say in which file
and in which line of that file did we provide the incorrect value.

Besides <hl>Int</hl>, there are a number of other types supplied by
<a href="http://metacpan.org/module/Types::Standard">Types::Standard</a>.

<h2>Use class-name as constraint</h2>

Besides the predefined types, we can also use the name of any class as a
type-constraint. For example we might want to have a <hl>birthdate</hl>
that is a <a href="http://metacpan.org/module/DateTime">DateTime</a> object.
We can change the <hl>Person.pm</hl> file to have

<code lang="perl">
has birthdate => (is => 'ro', isa => 'DateTime');
</code>

Then if we change our script and write

<code lang="perl">
my $student = Person->new(name => 'Foo', birthdate => '1987-12-18');
</code>

We will get an exception:

<hl>value "1987-12-18" did not pass type constraint (not isa DateTime) (in $self->{"birthdate"}) at</hl>

Again, the error message describes the problem well, but does not say where should we look to fix it.
If we change the script to the following:

<code>
use DateTime;
my $student = Person->new(
    name => 'Foo',
    birthdate => DateTime->new(year => 1987, month => 12, day => 18),
);
say $student->name;
say $student->birthdate;
</code>

It will work and we will get the following output:

<code>
Foo
1987-12-18T00:00:00
</code>

The stringified value of DateTime object contains the hours, minutes and seconds as well, 
that we have not set so they default to 0.
We can get DateTime to print only the Year/Month/Day part or in short <hl>ymd</hl>:

<code lang="perl">
say $student->birthdate->ymd; # 1987-12-18'
</code>

Will print <hl>1987-12-18</hl>.

<h2>Our class as a type constraint</h2>

Any class-name can be used as type constraint. The class can come in the form of a CPAN module,
or it can be one we created for our project. A class can even use itself as one of the constraints.
For example, each Person has a mother, who is also a Person. We change the <hl>Person.pm</hl> file
to have the following:

<code lang="perl">
has mother => (is => 'rw', isa => 'Person');
</code>

and the <hl>person.pl</hl> script to have this:

<code lang="perl">
my $student = Person->new(name => 'Foo', mother => 'Bar');
</code>

Running the script will give an exception:
<hl>value "Bar" did not pass type constraint (not isa Person) (in $self->{"mother"})</hl>
That's because we were expecting an object of type "Person" and not just a string.
Let's try that by changing our script:

<code lang="perl">
my $visitor = Person->new(name => 'Bar');
my $student = Person->new(name => 'Foo', mother => $visitor);
say $student->name;
say $student->mother->name;
</code>

First we create the object representing the mother, who is visiting her child,
hence the variable is called <hl>$visitor</hl>, then we pass this new object to
the constructor.

The output is what you might have expected:

<code>
Foo
Bar
</code>

