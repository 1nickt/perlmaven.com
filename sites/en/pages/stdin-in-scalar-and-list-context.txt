=title STDIN in scalar and list context
=timestamp 2015-03-18T09:45:56
=indexes STDIN, <STDIN>, scalar
=status show
=books beginner_book
=author szabgab
=comments_disqus_enable 1

=abstract start

We have learned about <a href="/scalar-and-list-context-in-perl">scalar and list context in Perl</a>, and we have seen
how does <a href="/the-year-19100">localtime behave in scalar and list context</a>. We also saw what does
and array return in scalar context.
We also saw how to <a href="">read from a file in SCALAR and LIST context</a>.

This time we'll look at reading from STDIN using <hl><STDIN></hl> in scalar and list context.

=abstract end


<h2>STDIN in scalar context</h2>

This is what we have have been doing right from the 
<a href="/installing-perl-and-getting-started">beginning</a> of the <a href="/perl-tutorial">Perl Tutorial</a>.

<code lang="perl">
use strict;
use warnings;
 
print "Please type in your name: ";
my $name = <STDIN>;
chomp $name;
print "Hello $name, how are you?\n";
</code>

The relevant part is that we read from STDIN and assign to a scalar variable <hl>$name</hl>.

If we run this script, it will print "Please type in your name: " and then it will wait till we press <b>ENTER</b>.
Then it will take everything we type, including the ENTER at the end, and assign it to the <hl>$name</hl> variable.
<hl>chomp</hl> will eliminate the trailing newlines.


<h2>STDIN in list context</h2>

If we assign the <hl><STDIN></hl> readline operator to an array it means we put the readline operator in LIST context.
Let's see this example:

<code lang="perl">
use strict;
use warnings;
 
print "Please type in the names of the programming languages you know: ";
my @names = <STDIN>;

chomp @names;
print "Hello, I see you know " . scalar(@names) . "\n";
</code>

This script prints some text too and then waits for your input. Pressing ENTER after you typed in some text
will not end the waiting of Perl. In order to tell Perl that you have finished typing you need to press
ENTER (to get to the next empty line) and then send and <hl>EOF</hl> a.k.a. <hl>End of File</hl> signal.
How to send that is easy, but it depend on the Operating system. On Linux/Unix system pressing <hl>Ctrl-D</hl>
(also written as ^D sometimes) will send this signal.
On MS Windows you have to type <hl>Ctrl-Z</hl> (sometimes written as ^Z) and then also press <hl>ENTER</hl>.

In both cases you have to send the signal on a new line, that is, after pressing ENTER.

Once you send that signal, perl will take everything you typed and assign to the <hl>@names</hl> array.
Every line you typed in, every string that you finished by pressing ENTER
will be in a separate element of the array. So, if you type in:

<code>
Please type in the names of the programming languages you know:
JavaENTER
PerlENTER
PythonENTER
^D
</code>

The array will have 3 elements like these:

<code lang="perl">
@names = (
   "Java\n",
   "Perl\n",
   "Python\n",
);
</code>

Then we apply <hl>chomp</hl> to all of them at once removing all the trailing newlines and getting:

<code lang="perl">
@names = (
   "Java",
   "Perl",
   "Python",
);
</code>

On the last line we print out the <a href="/length-of-an-array">number of elements in the array</a>.


<h2>Usage?</h2>

Almost always you need to read from STDIN in scalar context,
that is, assigning the result to a scalar variable: <hl>$input = <STDIN>;</hl>.


